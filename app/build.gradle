import org.apache.commons.codec.binary.Base64

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def stringLiteral(String s) {
    if (s == null) {
        return "null"
    } else {
        def encoded = Base64.encodeBase64(s.getBytes())
        return "new String(android.util.Base64.decode(new byte[]{${encoded.toList().join(",")}}, android.util.Base64.DEFAULT)) /* ${s} */"
    }
}

android {
    compileSdkVersion rootProject.compileSdkVersion
    defaultConfig {
        applicationId "com.ishikota.dribbbleclientandroid"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // Define static consts for app
        buildConfigField "String", "DRIBBBLE_OAUTH_ENDPOINT",
                stringLiteral("https://dribbble.com/")
        buildConfigField "String", "DRIBBBLE_OAUTH_REDIRECT_URI",
                stringLiteral("ishikota://callback")
        buildConfigField "String", "DRIBBBLE_CLIENT_ID",
                stringLiteral("38efc465078722414adbedbfa2f43dd41b93178096489b85e5a6a81f2fa788e4")
        buildConfigField "String", "DRIBBBLE_CLIENT_SECRET",
                stringLiteral("TODO replace me")
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"

    implementation "androidx.appcompat:appcompat:$appcompatVersion"
    implementation "androidx.core:core-ktx:$coreVersionn"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayoutVersion"

    implementation "io.reactivex.rxjava2:rxjava:$rxjavaVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$rxandroidVersion"

    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"
    implementation "com.squareup.moshi:moshi:$moshiversion"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshiversion"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofitVersion"

    testImplementation "junit:junit:$junitVersion"

    androidTestImplementation "androidx.test.ext:junit:$textExtVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
}
